// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

#define MAX_STEPS 100
#define MAX_DIST 100
#define SURF_DIST 1e-3

Texture2D<float4> Source;
RWTexture2D<float4> Result;
float4x4 _CameraToWorld, _CameraInverseProjection;
float3 _WorldSpaceCameraPos;
float _Gloss, _Scale, _BlendStrength;
int _Iterations;

struct LightData
{
    float3 position;
    float3 direction;
    float4 color;
    float intensity;
    float spotAngle;
    int type;
};
StructuredBuffer<LightData> lightsBuffer;

struct Ray
{
    float3 origin;
    float3 direction;
};

Ray CreateRay(float3 origin, float3 direction) {
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    return ray;
}

Ray CreateCameraRay(float2 uv) {
    float3 origin = mul(_CameraToWorld, float4(0, 0, 0, 1)).xyz;
    float3 direction = mul(_CameraInverseProjection, float4(uv, 0, 1)).xyz;
    direction = mul(_CameraToWorld, float4(direction, 0)).xyz;
    direction = normalize(direction);
    return CreateRay(origin, direction);
}

//Palette Creator
float3 GetPaletteColor(in float t) {
    //Palette
    float3 PALETTE_BIAS = float3(.5, .5, .5);
    float3 PALETTE_AMPLITUDE = float3(.5, .5, .5);
    float3 PALETTE_FREQUENCIES = float3(1, .2, .3);
    float3 PALETTE_OFFSET = float3(0, .15, .2);
    float PALETTE_G_FREQUENCY = 1.f;
    float PALETTE_G_OFFSET = .1f;

    return PALETTE_BIAS + PALETTE_AMPLITUDE * cos(3.14 * ( PALETTE_G_FREQUENCY * PALETTE_FREQUENCIES * t + PALETTE_OFFSET + PALETTE_G_OFFSET));
    //return float3(t,t,t);
}

void opSmoothUnionColor(inout float d1, float d2, inout float3 colorA, float3 colorB, float blendStrength)
{
    float h = clamp(0.5 + 0.5 * (d2 - d1) / blendStrength, 0.0, 1.0);
    float blendDst = lerp(d2, d1, h) - blendStrength * h * (1.0 - h);
    float3 blendCol = lerp(colorB, colorA, h);

    d1 = blendDst;
    colorA = blendCol;
}

float sdTetrahedron(float3 p)
{
    return 
        (
            max
            (
                max(-p.x - p.y - p.z, p.x + p.y - p.z),
                max(-p.x + p.y + p.z, p.x - p.y + p.z)
            )
            - 1.
        ) / sqrt(3.0);
}

float2 SierpinskiTetrahedron(float3 z) 
{
    float3 n1 = normalize(float3(1, 1, 0));
    float3 n2 = normalize(float3(1, 0, 1));
    float3 n3 = normalize(float3(0, 1, 1));

    float s = 1.;
    float orbit = 1e20;
    for (int n = 0; n < _Iterations; n++) {
        orbit = min(orbit, dot(z, z));

        //tetrahedron folding
        z -= 2. * min(0., dot(z, n1)) * n1;
        z -= 2. * min(0., dot(z, n2)) * n2;
        z -= 2. * min(0., dot(z, n3)) * n3;

        //scaling
        z = z * _Scale - 1.;
        s /= _Scale;
    }
    //dis & descale
    return float2(sdTetrahedron(z) * s, orbit);
}

float4 DistanceEstimator(float3 pos)
{
    float plane = pos.y - 0;

    // Translate
    pos.y -= 1;
    float2 fractal = SierpinskiTetrahedron(pos);

    //float dst = min(fractal.x, plane);
    float dst = fractal.x;
    float3 color = GetPaletteColor(fractal.y);
    /*if (fractal.x < plane)
    {
        dst = fractal.x;
        color = GetPaletteColor(fractal.y);
    }
    else
    {
        dst = plane;
        color = float3(.9, .9, .9);
    }*/

    opSmoothUnionColor(dst, plane, color, float3(.9, .9, .9), _BlendStrength);

    return float4(dst, color);
}

float2 RayMarch(float3 ro, float3 rd)
{
    float dO = 0;   // Dist from origin
    float dS;   // Dist from surface
    int i;

    // March the ray for a limited amount of steps
    for (i = 0; i < MAX_STEPS; i++)
    {
        // Calculate the current position
        float3 p = ro + dO * rd;

        // Get the distance to the scene
        float DE = DistanceEstimator(p).x;
        dS = DE;
        dO += dS;
        if (dS<SURF_DIST || dO>MAX_DIST) break;
    }

    return float2(dO, 1.0 - float(i) / float(MAX_STEPS));
}

float3 GetNormal(float3 pos)
{
    float2 e = float2(SURF_DIST, 0);
    float3 n = DistanceEstimator(pos).x - float3
        (
            DistanceEstimator(pos - e.xyy).x,
            DistanceEstimator(pos - e.yxy).x,
            DistanceEstimator(pos - e.yyx).x
            );
    return normalize(n);
}

//float CalculateAO(float3 p)
//{
//    float sum = 0.f;
//    float k = 2.f;
//
//    for (int i = 0; i < 5; i++)
//    {
//        sum += 1 / (pow(2, i)) * ();
//    }
//
//    return 1 - k * sum;
//}

float CalculateShadows(in float3 ro, in float3 rd, float mint, float maxt)
{
    for (float t = mint; t < maxt; )
    {
        float h = DistanceEstimator(ro + rd * t).x;
        if (h < SURF_DIST)
            return 0;
        t += h;
    }
    return 1.0;
}

float3 CalculateBlinnPhongSpecular(float3 p, float3 normal, float3 lightDirection)
{
    // Set the specular colour to white
    float3 specularColour = float3(1, 1, 1);

    // Calculate the view vector
    float3 fragToCam = _WorldSpaceCameraPos - p;
    float3 viewVector = normalize(fragToCam);

    // Get the vector that is halfway between the light direction and the view vector
    float3 halfwayVec = normalize(lightDirection + viewVector);

    // Calculate the intensity/falloff of the specular highlight
    float specularIntensity = pow(max(0, dot(normal, halfwayVec)), _Gloss);

    return saturate(specularColour * specularIntensity);
}

float3 CalculateDirectionalLight(float3 p, LightData light)
{
    float3 normal = GetNormal(p);

    // Calculate light falloff and colour
    float lightFallOff = max(0, dot(-light.direction, normal) * light.intensity);
    float3 lightColor = light.color.xyz * lightFallOff;

    // Calculate specular
    float3 specular = CalculateBlinnPhongSpecular(p, GetNormal(p), -light.direction) * lightColor;

    // Calculate shadows
    float shadow = CalculateShadows(p, -light.direction, 5e-2, MAX_DIST);

    return saturate(lightColor + specular) * shadow;
}

float3 CalculatePointLight(float3 p, LightData light)
{
    float3 normal = GetNormal(p);

    // Calculate light falloff and colour
    float3 lightDir = normalize(light.position - p);
    float lightFallOff = max(0, dot(lightDir, normal) * light.intensity);
    float3 lightColor = light.color.xyz * lightFallOff;

    // Calculate shadows
    float shadow = CalculateShadows(p, lightDir, 5e-2, MAX_DIST);

    return lightColor * shadow;
}

float3 CalculateSpotLight(float3 p, LightData light)
{
    float3 normal = GetNormal(p);

    float3 lightVec = normalize(light.position - p);
    float cutOff = saturate(dot(-lightVec, light.direction));

    float angle = light.spotAngle / 2 * 3.1415f / 180.f;
    if (cutOff < cos(angle))
    {
        return 0;
    }

    float lightFallOff = max(0, dot(lightVec, normal) * light.intensity);
    float3 lightColor = light.color.xyz * lightFallOff;

    // Calculate shadows
    float shadow = CalculateShadows(p, lightVec, 5e-2, MAX_DIST);

    return lightColor * shadow;
}

float3 GetLightColor(float3 p, LightData light)
{
    float3 finalColor = 0;
    float3 ambientLight = float3(0.2, 0.2, 0.2);

    // Get the light type
    switch (light.type)
    {
    case 0: // SPOT
    {
        finalColor = CalculateSpotLight(p, light);
        break;
    }
    case 1: // DIRECTIONAL
    {
        finalColor = CalculateDirectionalLight(p, light);
        break;
    }
    case 2: // POINT
    {
        finalColor = CalculatePointLight(p, light);
        break;
    }
    }
    return finalColor + ambientLight;
}

[numthreads(8, 8, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    // Get the UVs of the screen
    uint width, height;
    Result.GetDimensions(width, height);
    float2 uv = id.xy / float2(width, height) * 2 - 1;

    // Set the colour to the source render texture initially
    float4 col = Source[id.xy];
    Result[id.xy] = col;

    // Calculate the ray origin and direction
    Ray cameraRay = CreateCameraRay(uv);
    float3 ro = cameraRay.origin;
    float3 rd = cameraRay.direction;

    // March the ray from the origin in the ray direction and retrieve the distance to the closest object hit
    float2 rayMarch = RayMarch(ro, rd);
    float dist = rayMarch.x;

    // If the distance was greater than or equal to the max dist then nothing was hit so return
    if (dist >= MAX_DIST)
        return;

    // Get the point of intersection
    float3 p = ro + rd * dist;
    float3 normal = GetNormal(p);
    col.rgb = GetLightColor(p, lightsBuffer[0]) * rayMarch.y * DistanceEstimator(p).gba;
    //col.rbga = dist;

    // Set the pixel of the final render texture to the color
    Result[id.xy] = col;
}
